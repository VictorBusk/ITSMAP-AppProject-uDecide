plugins {
    id "org.sonarqube" version "2.6.1"
}

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def sonarProjectPropertiesFile = rootProject.file("sonar-project.properties")
def sonarProjectProperties = new Properties()
sonarProjectProperties.load(new FileInputStream(sonarProjectPropertiesFile))

ext {
    androidKeyStoreFile = keystoreProperties['storeFile'] ?: String.valueOf(System.getenv("ANDROID_KEY_STORE_FILE"))
    androidKeyStorePassword = keystoreProperties['storePassword'] ?: System.getenv("ANDROID_KEY_STORE_PASSWORD")
    androidKeyAlias = keystoreProperties['keyAlias'] ?: System.getenv("ANDROID_KEY_ALIAS")
    androidKeyPassword = keystoreProperties['keyPassword'] ?: System.getenv("ANDROID_KEY_PASSWORD")

    sonarHostUrl = sonarProjectProperties['sonar.host.url'] ?: System.getenv("SONAR_HOST_URL")
    sonarOrganization = sonarProjectProperties['sonar.organization'] ?: System.getenv("SONAR_ORGANIZATION")
    sonarLogin = sonarProjectProperties['sonar.login'] ?: System.getenv("SONAR_LOGIN")
}

android {
    signingConfigs {
        sign {
            storeFile file(androidKeyStoreFile)
            storePassword androidKeyStorePassword
            keyAlias androidKeyAlias
            keyPassword androidKeyPassword
        }
    }
    compileSdkVersion 27
    defaultConfig {
        multiDexEnabled true
        applicationId "dk.au.ase.itsmap.e17.appproject.gruppe7.udecide"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 2
        versionName "2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
        debug {
            signingConfig signingConfigs.sign
        }
    }
}

sonarqube {
    properties {
        property "sonar.host.url", sonarHostUrl
        property "sonar.organization", sonarOrganization
        property "sonar.login", sonarLogin
    }
}

ext {
    supportLibraryVersion = '27.0.2'
    firebaseLibraryVersion = '11.8.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:animated-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseLibraryVersion"
    implementation "com.google.firebase:firebase-firestore:$firebaseLibraryVersion"
    implementation "com.google.firebase:firebase-storage:$firebaseLibraryVersion"
    implementation 'com.facebook.android:facebook-android-sdk:4.29.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

apply plugin: 'com.google.gms.google-services'
