plugins {
    id "org.sonarqube" version "2.6.1"
}

apply plugin: 'com.android.application'

ext {
    if (System.getenv("CIRCLECI")) {
        androidKeyStoreFile = String.valueOf(System.getenv("ANDROID_KEY_STORE_FILE"))
        androidKeyStorePassword = System.getenv("ANDROID_KEY_STORE_PASSWORD")
        androidKeyAlias = System.getenv("ANDROID_KEY_ALIAS")
        androidKeyPassword = System.getenv("ANDROID_KEY_PASSWORD")
        sonarHostUrl = System.getenv("SONAR_HOST_URL")
        sonarOrganization = System.getenv("SONAR_ORGANIZATION")
        sonarLogin = System.getenv("SONAR_LOGIN")
    } else {
        def keystorePropertiesFile = rootProject.file("keystore.properties")
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        androidKeyStoreFile = keystoreProperties['storeFile']
        androidKeyStorePassword = keystoreProperties['storePassword']
        androidKeyAlias = keystoreProperties['keyAlias']
        androidKeyPassword = keystoreProperties['keyPassword']
        sonarHostUrl = System.getenv("SONAR_HOST_URL")
        sonarOrganization = System.getenv("SONAR_ORGANIZATION")
        sonarLogin = System.getenv("SONAR_LOGIN")
    }
}

android {
    signingConfigs {
        sign {
            storeFile file(androidKeyStoreFile)
            storePassword androidKeyStorePassword
            keyAlias androidKeyAlias
            keyPassword androidKeyPassword
        }
    }
    compileSdkVersion 27
    defaultConfig {
        multiDexEnabled true
        applicationId "dk.au.ase.itsmap.e17.appproject.gruppe7.udecide"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 3
        versionName "2.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
        debug {
            signingConfig signingConfigs.sign
        }
    }
}

sonarqube {
    properties {
        property "sonar.host.url", sonarHostUrl
        property "sonar.organization", sonarOrganization
        property "sonar.login", sonarLogin
        property "sonar.projectKey", "gruppe7:uDecide"
    }
}

ext {
    supportLibraryVersion = '27.0.2'
    firebaseLibraryVersion = '11.8.0'
    facebookLibraryVersion = '4.29.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

//    v4 Support Libraries
    implementation "com.android.support:support-compat:$supportLibraryVersion"
    implementation "com.android.support:support-core-utils:$supportLibraryVersion"
    implementation "com.android.support:support-core-ui:$supportLibraryVersion"
    implementation "com.android.support:support-fragment:$supportLibraryVersion"

//    v7 Support Libraries
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"

//    v13 Support Library
    implementation "com.android.support:support-v13:$supportLibraryVersion"

//    Support Libraries
    implementation "com.android.support:support-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:animated-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

//    Firebase
    implementation "com.google.firebase:firebase-auth:$firebaseLibraryVersion"
    implementation "com.google.firebase:firebase-firestore:$firebaseLibraryVersion"
    implementation "com.google.firebase:firebase-storage:$firebaseLibraryVersion"

//    Facebook
    implementation "com.facebook.android:facebook-android-sdk:$facebookLibraryVersion"

//    Picasso
    implementation 'com.squareup.picasso:picasso:2.5.2'

//    Test Libraries
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

apply plugin: 'com.google.gms.google-services'
